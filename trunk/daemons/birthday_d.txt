// Last edited by deforce on 01-27-2007
inherit M_DAEMON_DATA;

mapping birthdays = ([]);
mapping reverse = ([]);

nosave string today;

void refresh()
{
   today = ctime(time())[4..9];
}

void repeat_refresh()
{
   today = ctime(time())[4..9];
   call_out((:repeat_refresh:), 86400);
}

varargs string array query_birthdays(string when)
{
   mixed item;

   refresh();
   item = birthdays[when || today];
   return item ? explode(item, ",") : ({ });
}

varargs mapping query_month(string month)
{
   mapping output = ([]);
   int count;

   if (!month) { month = today[0..2]; }

   for (count = 1; count <= 10; count++)
   {
      string temp = birthdays[month + "  " + count];

      if (temp) { output[month + "  " + count] = temp; }
   }

   for (count = 10; count <= 31; count++)
   {
      string temp = birthdays[month + " " + count];

      if (temp) { output[month + " " + count] = temp; }
   }

   return output;
}

void add_birthday(string name, string when)
{
   if (strlen(when) == 5)
   {
      when = when[0..2] + " " + when[3..4];
   }
   else if (strlen(when) != 6 || when[3] != ' ')
   {
      error("Bad date format\n");
   }

   switch (when[0..2])
   {
      case "Jan": case "Feb": case "Mar": case "Apr": case "May": case "Jun":
      case "Jul": case "Aug": case "Sep": case "Oct": case "Nov": case "Dec":
         break;
      case "jan": case "feb": case "mar": case "apr": case "may": case "jun":
      case "jul": case "aug": case "sep": case "oct": case "nov": case "dec":
         when = capitalize(when);
         break;
      default:
         error("Bad date format\n");
   }

   if ((member_array(when[4], " 0123456789") == -1) || (member_array(when[5], "0123456789") == -1))
   {
      error("Bad date format\n");
   }
   else if (when[4] == '0')
   {
      when[4] = ' ';
   }
   else if (member_array(',', name) != -1)
   {
      error("Bad name\n");
   }

   if (reverse[name])
   {
      mixed temp = explode(birthdays[reverse[name]], ",");
      temp -= ({ name });

      birthdays[reverse[name]] = implode(temp, ",");
   }

   reverse[name] = when;

   if (birthdays[when])
   {
      birthdays[when] += "," + name;
   }
   else
   {
      birthdays[when] = name;
   }

   save_me();
}

void report()
{
   string array bdays = sort_array(query_birthdays(), 1);

   if (!sizeof(bdays)) { return; }

   bdays = map(bdays, (: capitalize :));

   printf("Today's birthdays: %s.\n\n", implode(bdays, ", "));
}


void create()
{
   ::create();
}