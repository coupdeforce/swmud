// Last edited by deforce on 03-04-2007
inherit CMD;

string format_time_left(int time);

private void main(string arg)
{
   object body = find_body(arg);
   mapping delays = ([ ]);

   if (!wizardp(this_body()) || !body) { body = this_body(); }

   delays = body->query_skill_delays();

   if (sizeof(keys(delays)))
   {
      int time = time();
      string output = "";

      foreach (string skill in sort_array(keys(delays), 1))
      {
         output += sprintf("%-30s : %s\n", skill, format_time_left(delays[skill] - time));
      }

      out(output + "\n");
   }
   else
   {
      out("You have no skill delays.\n");
   }
}

string format_time_left(int time)
{
   string output = "";

   if (time > 59)
   {
      if (time > 3599)
      {
         if (time > 86399)
         {
            if ((time / 86400) > 0) { output += (time / 86400) + " days, "; }

            time = time % 86400;
         }

         if ((time / 3600) > 0) { output += (time / 3600) + " hours, "; }

         time = time % 3600;
      }

      if ((time / 60) > 0) { output += (time / 60) + " minutes, "; }
   }

   output += (time % 60) + " seconds";

   return output;
}